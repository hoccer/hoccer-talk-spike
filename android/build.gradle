buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.application'

if (!hasProperty("label")) ext.label = "hoccer"
if (!hasProperty("environment")) ext.environment = "development"

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.artcom.hoccer"
        minSdkVersion 14
        targetSdkVersion 22 // when updating 'targetSdkVersion' be aware of issues with the xzing library and Android 6.+
        versionCode 413
        versionName "3.2.10-SNAPSHOT"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("$projectDir/config/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            Properties buildProperties = new Properties()
            buildProperties.load(new FileInputStream("$projectDir/label/${label}/environments/production/build.properties"))
            Properties configProperties = new Properties()
            configProperties.load(new FileInputStream("$projectDir/label/${label}/environments/production/assets/configuration.properties"))
            manifestPlaceholders = [
                    "hoccer.invitation.uri.scheme": configProperties.getProperty("hoccer.invitation.uri.scheme"),
                    "app.name"                    : buildProperties.getProperty("app.name"),
                    "app.name.short"              : buildProperties.getProperty("app.name.short"),
                    "apk.package.name"            : buildProperties.getProperty("apk.package.name"),
                    "google.maps.api.key"         : buildProperties.getProperty("google.maps.api.key")
            ]
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg', 'proguard-release.cfg'
            zipAlignEnabled false
            debuggable false
            resValue "string", "package_name", buildProperties.getProperty("apk.package.name")
        }

        debug {
            Properties buildProperties = new Properties()
            buildProperties.load(new FileInputStream("$projectDir/label/${label}/environments/development/build.properties"))
            Properties configProperties = new Properties()
            configProperties.load(new FileInputStream("$projectDir/label/${label}/environments/development/assets/configuration.properties"))
            manifestPlaceholders = [
                    "hoccer.invitation.uri.scheme": configProperties.getProperty("hoccer.invitation.uri.scheme"),
                    "app.name"                    : buildProperties.getProperty("app.name"),
                    "app.name.short"              : buildProperties.getProperty("app.name.short"),
                    "apk.package.name"            : buildProperties.getProperty("apk.package.name"),
                    "google.maps.api.key"         : buildProperties.getProperty("google.maps.api.key")
            ]
            applicationIdSuffix ".dev"
            debuggable true
            signingConfig signingConfigs.debug
            resValue "string", "package_name", buildProperties.getProperty("apk.package.name")
        }

        review {
            Properties buildProperties = new Properties()
            buildProperties.load(new FileInputStream("$projectDir/label/${label}/environments/testing/build.properties"))
            Properties configProperties = new Properties()
            configProperties.load(new FileInputStream("$projectDir/label/${label}/environments/testing/assets/configuration.properties"))
            manifestPlaceholders = [
                    "hoccer.invitation.uri.scheme": configProperties.getProperty("hoccer.invitation.uri.scheme"),
                    "app.name"                    : buildProperties.getProperty("app.name"),
                    "app.name.short"              : buildProperties.getProperty("app.name.short"),
                    "apk.package.name"            : buildProperties.getProperty("apk.package.name"),
                    "google.maps.api.key"         : buildProperties.getProperty("google.maps.api.key")
            ]
            minifyEnabled true
            applicationIdSuffix ".test"
            debuggable true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg', 'proguard-release.cfg'
            resValue "string", "package_name", buildProperties.getProperty("apk.package.name")
        }
    }
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    productFlavors {
        hoccer {
            applicationId "com.artcom.hoccer"
        }
    }
}
group = 'hoccer'
version = '3.2.10-SNAPSHOT'

description = "Hoccer Talk for Android"
repositories {
    maven { url "https://archiva.hoccer.com/repository/snapshots/" }
    maven { url "https://archiva.hoccer.com/repository/hoccer/" }
    maven { url "http://repo.maven.apache.org/maven2" }
    jcenter()
}

dependencies {
    compile(project(':hoccer-talk-client')) {
        exclude group: 'log4j'
    }
    compile project(':drag-sort-listview')
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile('com.googlecode.ez-vcard:ez-vcard:0.8.3') {
        exclude(module: 'jsoup')
        exclude(module: 'freemarker')
    }
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.0'
    compile 'com.j256.ormlite:ormlite-android:4.43'
    compile 'ufwa-common:log4j-android:1.2.17-1'
    compile 'commons-codec:commons-codec:1.7'
    compile 'net.hockeyapp.android:HockeySDK:3.0.1'
    compile 'com.google.zxing:core:3.2.1'
    compile 'inter.nal.barcodescanner:zbar:1.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.1.5'
    compile 'net.lingala.zip4j:zip4j:1.3.2'
    testCompile 'junit:junit:4.11'
    testCompile 'com.h2database:h2:1.4.179'
    testCompile 'com.j256.ormlite:ormlite-jdbc:4.43'
    testCompile 'com.jayway.awaitility:awaitility:1.6.3'
}
